grammar org.xtext.workshop.WebGui with org.eclipse.xtext.common.Terminals

generate webGui "http://www.xtext.org/workshop/WebGui"


Model:
    'model' name=ID
        domain=DomainModel
        web=WebModel
    ;

/*
 * +--------------+
 * | Domain model |
 * +--------------+
 */

DomainModel:
    {DomainModel}	// ensure model.domain != null
    'domain:'
        (entities+=Entity)*
        (dataTypes+=DataType)*
    ;

Entity:
    'entity' name=ID 
    '{'
        (features+=Feature)*
    '}';

Feature:
    name=ID ':' type=[Type]
    	( multivalued ?= ( '(s)' | '(es)' ) )?;

Type:
    Entity | DataType;
    // convenience super type
    // 	(does not correspond to a particular language construct)

DataType:
    'type' name=ID;


/*
 * +-----------+
 * | Web model |
 * +-----------+
 */

WebModel:
	{WebModel}	// ensure model.web != null
    'web:'
    	(pages+=Page)*
    ;

Page:
    'page' name=ID
    '{'
        'title' '=' title=STRING
        'content for' entity=[Entity]
        '{'
            (contents+=PageElement)*
            
        '}'
    '}';
    // (initial) scope for PageElement-s: features of referenced Entity

PageElement:
    SingleElement | RepeatElement;

SingleElement:
    ActionElement | DisplayElement | InputElement;

ActionElement:	// ~ button
    'action' name=ID;

DisplayElement:	// ~ display field
    'show' reference=[Feature];
    // scope is narrowed to referenced Feature

InputElement:	// ~ input field
    'input for' reference=[Feature];
    // scope is narrowed to referenced Feature

RepeatElement:	// ~ for loop
    'repeat for' reference=[Feature] 
    '{'
        (contents+=SingleElement)*
    '}';
    // scope is narrowed to referenced Feature


/*  
DomainPath:
    head=DomainPathHead;
    
DomainPathHead:
    reference=[Feature]  tail=DomainPathTail;
    
DomainPathTail:
    '.' reference=[Feature] (tail=DomainPathTail)?;
*/        
